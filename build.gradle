plugins {
    id 'org.openapi.generator' version '7.6.0'
    id 'java'
    id 'maven-publish'
    id 'pl.allegro.tech.build.axion-release' version '1.20.1'
}

group = 'com.velaris.api'

scmVersion {
    tag {
        prefix = 'v'
    }
    versionIncrementer 'incrementPatch'
}

project.version = scmVersion.version

repositories {
    mavenCentral()
}

def generatedDir = layout.buildDirectory.dir("generated/api").get().asFile

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

sourceSets {
    main {
        java.srcDirs += file("${generatedDir}/src/main/java")
    }
}

dependencies {
    implementation(libs.jackson.databind.nullable)

    implementation(libs.spring.boot.starter.web)
    implementation(libs.spring.boot.starter.validation)

    compileOnly(libs.swagger.annotations)
    compileOnly(libs.springdoc.openapi.ui)
}

tasks.withType(JavaCompile).configureEach {
    dependsOn tasks.named("openApiGenerate")
}

tasks.named("compileJava") {
    dependsOn tasks.named("openApiGenerate")
}

tasks.withType(PublishToMavenRepository).configureEach {
    dependsOn tasks.named("openApiGenerate")
}

openApiGenerate {
    generatorName.set("spring")
    inputSpec.set("$rootDir/api/velaris-api.yaml")
    outputDir.set(generatedDir.toString())

    packageName.set("com.velaris")
    apiPackage.set("com.velaris.api")
    modelPackage.set("com.velaris.api.model")
    invokerPackage.set("com.velaris.api.invoker")

    configOptions.set([
            "interfaceOnly": "true",
            "skipDefaultInterface": "true",
            "dateLibrary": "java8",
            "useSpringBoot3": "true",
            "useTags": "true",
    ])
}

tasks.named("openApiGenerate") {
    doFirst {
        delete generatedDir
    }
}

tasks.register("generateServer") {
    dependsOn tasks.named("openApiGenerate")
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = "velaris-api"
            version = project.version
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Velaris-app/velaris-api")
            credentials {
                username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("gpr.user")
                password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.token")
            }
        }
    }
}